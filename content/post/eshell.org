#+TITLE: Making eshell your own
#+SLUG: customize-eshell
#+DATE: 2017-06-24
#+CATEGORIES: emacs
#+SUMMARY: Customizing the eshell prompt with icons and faces.
#+DRAFT: false

Include screenshots of different choices here.


#+BEGIN_SRC lisp
(defmacro with-face (STR &rest PROPS)
  "Return STR propertized with PROPS."
  `(propertize ,STR 'face (list ,@PROPS)))

(defmacro esh-section (NAME ICON STR &rest PROPS)
  "Return eshell section string with ICON header for STR with PROPS."
  `(setq ,NAME
         (lambda () (when ,STR
                      (-> ,ICON
                          (concat esh-section-delim ,STR)
                          (with-face ,@PROPS))))))

(defun esh-acc (acc x)
  "Accumulator for evaluating and concatenating esh-sections."
  (--if-let (funcall x)
      (if (s-blank? acc)
          it
        (concat acc esh-sep it))
    acc))

(defun esh-prompt-func ()
  "Build `eshell-prompt-function'"
  (concat esh-header
          (-reduce-from 'esh-acc "" eshell-funcs)
          "\n"
          eshell-prompt-string))

(esh-section esh-dir
             "\xf07c"  ; 
             (abbreviate-file-name (eshell/pwd))
             '(:foreground "gold" :bold ultra-bold :underline t))

(esh-section esh-git
             "\xe907"  ; 
             (magit-get-current-branch)
             '(:foreground "pink"))

(esh-section esh-python
             "\xe928"  ; 
             pyvenv-virtual-env-name)

(esh-section esh-clock
             "\xe192"  ; 
             (format-time-string "%H:%M" (current-time))
             '(:foreground "forest green"))

(setq esh-prompt-num 0)
(add-hook 'eshell-exit-hook (lambda () (setq esh-prompt-num 0)))
(advice-add 'eshell-send-input :before
            (lambda (&rest args) (setq esh-prompt-num (incf esh-prompt-num))))

(esh-section esh-num
             "\xf0c9"  ; 
             (number-to-string esh-prompt-num)
             '(:foreground "brown"))

(setq esh-sep "  ")
(setq esh-section-delim " ")
(setq esh-header "\n ")
(setq eshell-prompt-regexp " ")
(setq eshell-prompt-string " ")
(setq eshell-funcs (list esh-dir esh-git esh-python esh-clock esh-num))

(setq eshell-prompt-function 'esh-prompt-func)
#+END_SRC
